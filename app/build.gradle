apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "nu.yona.app"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 15
        versionName "1.0.6"
    }
    buildTypes {
        release {
            lintOptions {
                disable 'MissingTranslation'
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        app {
            assets.srcDirs = ['src/main/assets']
        }
    }

    testOptions {
        //this keeps junit (without robolectric) from failing when calling Log.<whatever> or TextUtils.<whatever>
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def apk = output.outputFile
            def appName = getAppName()
            def branch = getBranch().replaceAll(/\//, "-")
            def versionString = getVersionString()
            def buildNumber = getBuildNumber()
            def apkName = appName + '-' + branch + '-' + versionString + '.' + buildNumber

            if (variant.buildType.name == "release")
                apkName += "-RELEASE"

            output.outputFile = new File(apk.parent, apkName + '.apk')
            output.packageApplication.outputFile = new File(apk.parent, apkName + '-unaligned.apk')
        }
    }

    productFlavors {
        //build flavors:
        zdevelopment {
            // this is for developer testing, not send crash report on hocky app
        }
        zdev {
            //do nothing here, just merge in custom res - use this for QA Testing
        }
        zproduction {
            //do nothing here, just merge in custom res
        }
        zacceptance {
            //do nothing here, just merge in custom res
        }
        zuat {
        }
    }

    signingConfigs {
        release {
            storeFile file(System.getenv('YONA_KEYSTORE') + '/yona.jks')
            storePassword System.getenv('YONA_KEYSTORE_PASS')
            keyAlias System.getenv('YONA_KEYSTORE_ALIAS')
            keyPassword System.getenv('YONA_ALIAS_PASS')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    android.defaultConfig.versionName = getVersionString()
    android.defaultConfig.versionCode = Integer.valueOf(getBuildNumber())

    lintOptions {
        abortOnError false
    }
}

def getAppName() {
    def appName
    if ('APP_NAME' in project.ext.properties) {
        appName = project.ext.properties['APP_NAME']
    } else {
        appName = "Yona"
    }
}

def getBuildNumber() {
    def buildNumber

    if ('BUILD_NUMBER' in project.ext.properties) {
        buildNumber = project.ext.properties['BUILD_NUMBER']
    } else {
        buildNumber = "13"
    }
    return buildNumber
}

def getVersionString() {
    def versionString

    if ('VERSION_STRING' in project.ext.properties) {
        versionString = project.ext.properties['VERSION_STRING']
    } else {
        versionString = android.defaultConfig.versionName ?: "0.0.1"
    }
    return versionString
}


def getBranch() {
    def branch

    if ('BRANCH' in project.ext.properties) {
        branch = project.ext.properties['BRANCH']
    } else {
        branch = "develop"
    }

    return branch
}

dependencies {
    compile project(':libTimepicker')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/commons-codec-1.10.jar')
    compile files('libs/commons-io-2.4.jar')
    compile files('libs/commons-lang3-3.4.jar')
    compile 'com.android.support:design:23+'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    compile 'com.android.support:recyclerview-v7:23.2.0'
    compile 'joda-time:joda-time:2.9.3'
    //using for swipe layout on recyclerview
    compile 'com.daimajia.swipelayout:library:1.2.0@aar'
    //using for sticky header recyclerview
    compile 'com.timehop.stickyheadersrecyclerview:library:[0.4.3]@aar'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile project(path: ':openvpn')
    compile 'com.google.android.gms:play-services-analytics:9.2.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:8.4.2'
}
